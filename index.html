<html>
  <!--
ape-cljs-playground - Away from Preferred Editor (Clojure)Script Playground

This page allows you to simply try out Clojure forms on your mobile device in
moments when you don't have your laptop/desktop with preferred text editor at
hand.

Therefore, this little project aims to provide mobile-friendly Clojure editing.
To me this means (at least):
  - Parinfer!
  - kept in (local) storage to allow for quick and easy incremental work;
  - evaluate anything that's available in (the SCI subset of) ClojureScript,
    including macros within the browser;
  - all output (including errors) displayed on the page.

Quite a number of web-based/mobile-oriented Clojure editors exist, but I haven't
yet found any that support all of the above sub-goals 'out of the box',
particularly the storage part. So, that's the itch being scratched here.

Not particularly aiming for
 - performance (just spoiled by SCI's out-of-the-box speed I guess);
 - small download size.

The project's git repository can be found at
https://github.com/jurjanpaul/ape-cljs-playground.
The page is hosted at https://jurjanpaul.github.io/ape-cljs-playground.

Currently provides
 - run ClojureScript (per SCI / Scittle)
   - includes macro support out-of-the-box (might require an extra reload)
 - Parinfer (per parinfer-codemirror)
 - persistence (per browser's localStorage)
 - Reagent (not essential, but did not require any extra effort per Scittle,
   plus it definitely kickstarted some dogfeeding).
   To render, simply invoke the function `render-with-reagent` with a reagent
   component function.
 - maximum portability by being contained in a single HTML file
   (not al all essential, but nice as long as it's feasible in light of the
   remaining wishes)
 - trivial but very helpful: can be saved as an 'app' on phone's home screen.

Current annoyances
 - After (un)zooming&scrolling, the cursor position sometimes no longer
   corresponds to the actual insertion point;
 - Error logs don't provide position info (line and column);
 - Losing editor/viewport focus when deleting/commenting code.

Other wishes/TODOs (nothing planned):
 - history / undo + redo
 - on-screen footer keyboard with opening braces + other useful special chars
 - 'clear editor' button
 - multiple buffers
 - improved styling for mobile (just zoom and swipe/scroll a lot for now!);
 - linting
 - allow fetching existing code (gist) from url
 - support links with the code in a query parameter (tricky to get right in
   combination with the local storage, not wanting to overwrite any previously
   entered code without warning);
 - add deps (hardly possible in the context of Scittle?)
 - separate Scittle script's namespace from user entered code's namespace
 - save to gist or ... ?


Thanks to all library and tool builders who made it possible for me to throw
this together so lazily!

Feel free to customize and extend this file as desired. Of course, I would like
to hear if this turns out helpful to you in any way.

Jurjan-Paul Medema, August 2022


P.S. Some of the alternative mobile / in-browser Clojure(Script) 'playgrounds'
that I know of:
  * http://app.klipse.tech/
  * https://borkdude.github.io/sci.web/
  * https://clojurescript.io/
  * Replete (iPhone app)

-->
  <head>
    <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/lib/codemirror.css">
    <style type="text/css">
      html, body, div, p, pre, h3, textarea {
        box-sizing: border-box;
        margin: 0;
        padding: 0;
      }

      body {
        padding: 1px 4px;
        font-family: sans-serif;
      }

      h3 {
        margin-top: 1em;
      }

      .parinfer-error {
        background: #FFBEBE;
        color: black !important;
      }

      .parinfer-paren-trail {
        opacity: 0.4;
      }

      .CodeMirror {
        box-sizing: border-box;
        border: 1px solid #aaa;
        padding: 1px;
        padding-bottom: 1em;
      }

      .CodeMirror-matchingbracket {
        color: orange !important;
        font-weight: bold;
        background-color: lightgray;        ;
      }

      .output {
        font-family: monospace;
        padding: 4px 4px;
        padding-bottom: 1em;
        color: white;
        background-color: darkslateblue;
        font-size: small;
        overflow-y: scroll;
      }

      .copy-to-clipboard {
        margin: 4px;
      }

      .page-source {
        border-style: solid;
        border-width: 1px;
      }

      #user-app {
        padding-bottom: 2em;
      }
    </style>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/lib/codemirror.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/addon/edit/matchbrackets.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/mode/clojure/clojure.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/parinfer@3.12.0/parinfer.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/parinfer-codemirror@1.4.2/parinfer-codemirror.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.2.8/dist/scittle.js" type="application/javascript"></script>
    <script crossorigin src="https://unpkg.com/react@17/umd/react.production.min.js"></script>
    <script crossorigin src="https://unpkg.com/react-dom@17/umd/react-dom.production.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.2.8/dist/scittle.reagent.js" type="application/javascript"></script>
    <script type="application/x-scittle">
(require '[clojure.string :as string]
         '[reagent.core :as r]
         '[reagent.dom :as rdom])

(def preloaded-code
  "(defn answer []
  ;; Better not obfuscate like this
  (->> js/Math.PI int (nth (iterate #(* % (inc %)) 1))))

(defn the-answer []
  [:div
   [:p \"The answer is ...\ " (answer)]])

(render-with-reagent the-answer)

(answer)
")
;; end of preloaded-code

(def page-source (r/atom nil))
(def reagent-component (r/atom nil))
(def output (r/atom ""))
(defn print-to-output [s]
  (swap! output str s "\n"))

;; Originally went with just `binding` `*print-fn*` and `*print-err-fn*`, but
;; that failed to be sufficient for functions invoked (again) from Reagent.
(alter-var-root (var *print-fn*)
                (constantly print-to-output))
(alter-var-root (var *print-err-fn*)
                (constantly print-to-output))

(defn load-page-source []
  (-> (js/fetch js/window.location.href)
      (.then #(.text %))
      (.then #(reset! page-source %))))

(defn toggle-page-source []
  (if @page-source
    (reset! page-source nil)
    (load-page-source)))

(def code-mirror
  (atom nil))

(defn code []
  (when-let [cm @code-mirror]
    (.getValue cm)))

(defn copy-to-clipboard [s]
  (let [temp-textarea (js/document.createElement "textarea")]
    (.appendChild js/document.body temp-textarea)
    (set! (.-value temp-textarea) s)
    (.select temp-textarea)
    (js/document.execCommand "copy")
    (.removeChild js/document.body temp-textarea)))

(defn stored-code []
  (.getItem (.-localStorage js/window) "code"))

(defn store-code [code]
  (.setItem (.-localStorage js/window) "code" code))

(defn code-input-changed [_event]
  (-> (code) store-code))

(defn initialize-code-mirror []
  (reset! code-mirror
          (.fromTextArea js/CodeMirror
                         (.getElementById js/document "code-input")
                         #js {"mode" "clojure"
                              "lineNumbers" true
                              "matchBrackets" true}))
  (.on @code-mirror
       "change"
       (fn [_ change] (code-input-changed change)))
  (.setValue @code-mirror
             (or (stored-code)
                 preloaded-code))
  (.init js/parinferCodeMirror @code-mirror "smart" {}))

(defn -clear-output! []
  (reset! output "")
  (reset! reagent-component nil))

;; available to user (renders within div)
(defn render-with-reagent [component]
  (reset! reagent-component component))

(defn load-code []
  (let [c (code)]
    (try
      (-clear-output!)
      (let [value
            (js/scittle.core.eval_string c)]
        (print-to-output (pr-str value)))
      (catch :default e
        (println e)
        (when-let [stack (.-stack e)]
          (println stack))
        (throw e)))))

(defn editor []
  [:div {:class "wrapper"}
   [:h3
   {:on-click toggle-page-source}
    "ape-cljs-playground - Away from Preferred Editor Clojure(Script) Playground"]
   (when @page-source
     [:div.page-source
      [:pre @page-source]])
   [:textarea
    {:id "code-input"
     :cols 120
     :rows 20
     :onChange code-input-changed}
    (or (.getItem (.-localStorage js/window) "code")
        preloaded-code)]
   [:div {:id "editor-footer"}
    [:button
     {:on-click load-code}
     (if (string/blank? @output)
       "Load"
       "Reload")]
    [:button.copy-to-clipboard
     {:on-click #(copy-to-clipboard (code))}
     "Copy to clipboard"]]
   (when-not (string/blank? @output)
     [:div
      [:h3 "Output"]
      [:div.output
       [:pre @output]]])
   (when-let [component @reagent-component]
     [:div
      [:h3 "Render"]
      (if (vector? component)
        component
        [component])])])

(rdom/render [editor] (.getElementById js/document "editor"))
(initialize-code-mirror)
    </script>
  </head>
  <body>
    <div id="editor"></div>
  </body>
</html>
