<html>
  <!--
ape-cljs-playground - Away from Preferred Editor ClojureScript Playground

This page allows you to simply try out Clojure forms on your mobile device in
moments when you don't have your laptop/desktop with preferred text editor at
hand. (You can also, for example, use it to write and run solutions for all of
the Advent of Code puzzles, https://adventofcode.com/, as I did for the 2023
edition.)

Therefore, this little project aims to provide mobile-friendly Clojure editing.
To me this means (at least):
  - Parinfer!
  - kept in (local) storage to allow for quick and easy incremental work;
  - evaluate anything that's available in (the SCI subset of) ClojureScript,
    including macros within the browser;
  - all output (including errors) displayed on the page.

Quite a number of web-based/mobile-oriented Clojure editors exist, but I haven't
yet found any that support all of the above sub-goals 'out of the box',
particularly the storage part. So, that's the itch being scratched here.

Not particularly aiming for
 - performance (just spoiled by SCI's out-of-the-box speed I guess);
 - small download size.

The project's git repository can be found at
https://github.com/jurjanpaul/ape-cljs-playground.
The page is hosted at https://jurjanpaul.github.io/ape-cljs-playground.

Currently provides
 - run ClojureScript (per SCI / Scittle, https://github.com/babashka/scittle)
   - includes macro support out-of-the-box (might require an extra reload)
 - Parinfer (per codemirror6-parinfer at
   https://github.com/jurjanpaul/codemirror6-parinfer)
 - persistence (per browser's localStorage)
 - Reagent (not essential, but did not require any extra effort per Scittle,
   plus it definitely kickstarted some dogfeeding).
   To render, simply invoke the convenience function `render-with-reagent`
   with a reagent component function as parameter.
 - trivial, but very helpful: can be saved as an 'app' on phone's home screen,
   each 'app' automagically getting its own localStorage!
 - history / undo + redo
 - Share feature that encodes code in URL, protecting the default/main buffer
   by saving changes to a new localStorage buffer after opening such an URL.
   Note: As was to be expected, large programs will result in a huge URL that
   no (mobile) browser can actually open.

API
 - everything that Scittle exposes, including:
     - cljs.pprint as pprint
     - clojure.string as string
     - reagent.core as r
     - reagent.dom as rdom
 - render-with-reagent

Other wishes/TODOs (nothing planned in the near future):
 - smart indentation (worked on CodeMirror 5, but not on CodeMirror 6 yet);
 - on-screen footer keyboard with opening braces + other useful special chars
 - multiple buffers
   (Hint: by saving the page multiple times to my iPhone's Home screen I
   already get multiple buffers for free!)
 - improved styling for mobile (just zoom and swipe/scroll a lot for now!);
 - Storing the history to local storage as well (used to be implemented, but
   I removed it for now, as it was no longer working reliably after the upgrade
   to CodeMirror 6, without writing a lot more code to deal with StateEffect
   (de)serialisation);
 - allow fetching existing code (gist) from url; (I included an experimental
   function `load-from-url` that would do this, but it required a rather
   ugly full reload and broke for existing code on GitHub when Scittle moved
   to match `:cljs` in reader conditionals, with the loaded code having a
   transitive dependency on the the Google Closure library...)
 - add deps (seems hard/hardly possible)
 - separate Scittle script's namespace from user entered code's namespace
 - save to gist or ... ?
 - linting

All in all I feel this is a nice showcase of what is possible with Scittle!
Thanks to all library and tool builders who made it possible for me to throw
this together so lazily!

Feel free to customize and extend this file as desired. Of course, I would
like to hear if this turns out helpful to you in any way.

  Jurjan-Paul Medema
  Github: jurjanpaul
  Twitter: @jurjanpaul
  Clojurians Slack: @jurjanpaul

P.S. Some of the alternative mobile / in-browser Clojure(Script) 'playgrounds'
that I know of:
  * http://app.klipse.tech/
  * https://borkdude.github.io/sci.web/
  * https://clojurescript.io/
  * https://nextjournal.github.io/clojure-mode/#try-it
  * https://oakes.github.io/paren-soup/
  * https://squint-cljs.github.io/squint/
  * Replete (iPhone app)

('Ape' also refers to the fact that this can indeed be considered a poor copy or
derivative of other people's hard work.)
-->
  <head>
    <title>Away from Preferred Editor ClojureScript Playground</title>
    <meta name="apple-mobile-web-app-capable" content="yes">
    <meta name="mobile-web-app-capable" content="yes">
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <meta name="url" content="https://jurjanpaul.github.io/ape-cljs-playground">
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Open+Sans:wght@400;700&family=Source+Code+Pro&display=swap" rel="stylesheet">
    <style type="text/css">
      * {
        box-sizing: border-box;
        margin: 0;
        padding: 0;
      }

      body {
        padding: 4px 4px 2em;
        font-family: "Open Sans", sans-serif;
      }

      button {
        border-radius: 4px;
        padding: 2px 6px 2px 6px;
      }

      .cm-editor {
        box-sizing: border-box;
        resize: both;
        border: 1px solid #aaa;
        padding: 1px;
        min-height: 20em;
        max-height: 60vh;
      }

      .cm-editor, .cm-editor * {
         font-family: "Source Code Pro", monospace;
         font-size: 0.8rem;
      }

      .CodeMirror-matchingbracket {
        color: orange !important;
        font-weight: bold;
        background-color: lightgray;        ;
      }

      #output {
        font-family: "Source Code Pro", monospace;
        padding: 0.5em;
        padding-bottom: 1em;
        color: white;
        background-color: #120754;
        font-size: small;
        overflow-y: scroll;
        min-height: 10em;
        border-style: inset;
        border-width: 1px;
        border-radius: 5px;
        border-color: lightblue;
        resize: both;
      }

      .page-source {
        border-style: solid;
        border-width: 1px;
        padding: 0.5em;
        margin-bottom: 0.5em;
      }

      .pointer {
        cursor: pointer;
      }
    </style>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.22/dist/scittle.js" type="application/javascript"></script>
    <script crossorigin src="https://unpkg.com/react@17/umd/react.production.min.js"></script>
    <script crossorigin src="https://unpkg.com/react-dom@17/umd/react-dom.production.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.22/dist/scittle.reagent.js" type="application/javascript"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.22/dist/scittle.re-frame.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.22/dist/scittle.pprint.js" type="application/javascript"></script>
    <script type="importmap">
     {
       "imports": {
         "codemirror": "https://cdn.jsdelivr.net/npm/codemirror@6.0.1/dist/index.js",
         "@codemirror/commands": "https://cdn.jsdelivr.net/npm/@codemirror/commands@6.7.1/dist/index.js",
         "@codemirror/search": "https://cdn.jsdelivr.net/npm/@codemirror/search@6.5.8/dist/index.js",
         "@codemirror/autocomplete": "https://cdn.jsdelivr.net/npm/@codemirror/autocomplete@6.18.3/dist/index.js",
         "@codemirror/lint": "https://cdn.jsdelivr.net/npm/@codemirror/lint@6.8.4/dist/index.js",
         "crelt": "https://cdn.jsdelivr.net/npm/crelt@1.0.6/index.js",
         "@nextjournal/lang-clojure": "https://cdn.jsdelivr.net/npm/@nextjournal/lang-clojure@1.0.0/dist/index.js",
         "@nextjournal/lezer-clojure": "https://cdn.jsdelivr.net/npm/@nextjournal/lezer-clojure@1.0.0/dist/index.es.js",
         "@lezer/highlight": "https://cdn.jsdelivr.net/npm/@lezer/highlight@1.2.1/dist/index.js",
         "@lezer/lr": "https://cdn.jsdelivr.net/npm/@lezer/lr@1.4.2/dist/index.js",
         "@lezer/common": "https://cdn.jsdelivr.net/npm/@lezer/common@1.2.3/dist/index.js",
         "@codemirror/commands": "https://cdn.jsdelivr.net/npm/@codemirror/commands@6.7.1/dist/index.js",
         "@codemirror/language": "https://cdn.jsdelivr.net/npm/@codemirror/language@6.10.6/dist/index.js",
         "@codemirror/lint": "https://cdn.jsdelivr.net/npm/@codemirror/lint@6.8.4/dist/index.js",
         "@marijn/find-cluster-break": "https://cdn.jsdelivr.net/npm/@marijn/find-cluster-break@1.0.2",
         "@codemirror/state": "https://cdn.jsdelivr.net/npm/@codemirror/state@6.5.0/dist/index.js",
         "@codemirror/view": "https://cdn.jsdelivr.net/npm/@codemirror/view@6.35.0/dist/index.js",
         "@codemirror/merge": "https://cdn.jsdelivr.net/npm/@codemirror/merge@6.7.4/dist/index.js",
         "style-mod": "https://cdn.jsdelivr.net/npm/style-mod@4.1.2/src/style-mod.js",
         "w3c-keyname": "https://cdn.jsdelivr.net/npm/w3c-keyname@2.2.8/index.js",
         "@jurjanpaul/codemirror6-parinfer": "https://cdn.jsdelivr.net/npm/@jurjanpaul/codemirror6-parinfer@0.0.14/dist/cm6-parinfer.min.js"
       }
     }
   </script>
   <script type="module">
     import * as codemirror from 'codemirror';
     import * as cm_commands from '@codemirror/commands';
     import * as cm_language from '@codemirror/language';
     import * as cm_lint from '@codemirror/lint';
     import * as cm_view from '@codemirror/view';
     import * as cm_state from '@codemirror/state';
     import * as cm_merge from '@codemirror/merge';
     import * as lang_clojure from '@nextjournal/lang-clojure'
     import * as codemirror6_parinfer from '@jurjanpaul/codemirror6-parinfer'
     globalThis.codemirror = codemirror;
     globalThis.cm_commands = cm_commands;
     globalThis.cm_language = cm_language;
     globalThis.cm_lint = cm_lint;
     globalThis.cm_state = cm_state;
     globalThis.cm_view = cm_view;
     globalThis.cm_merge = cm_merge;
     globalThis.lang_clojure = lang_clojure;
     globalThis.codemirror6_parinfer = codemirror6_parinfer;
     scittle.core.eval_script_tags();
   </script>
   <script type="application/x-scittle">
(require '[cljs.pprint :as pprint]
         '[clojure.string :as string]
         '[reagent.core :as r]
         '[reagent.dom :as rdom]
         '[sci.core :as sci])

(def preloaded-code
  "(defn answer []
  ;; Better not obfuscate like this
  (->> js/Math.PI int (nth (iterate #(* % (inc %)) 1))))

(defn the-answer []
  [:div {:style {:font-family \"Georgia\"}}
   [:p \"The answer turns out to ðŸ ... \" (answer)]])

(render-with-reagent the-answer)

(answer)
")
;; end of preloaded-code

(def page-source (r/atom nil))
(def reagent-component (r/atom nil))
(def output (r/atom nil))
(defn print-to-output [s]
  (swap! output str s "\n"))

;; Originally went with just `binding` `*print-fn*` and `*print-err-fn*`, but
;; that failed to be sufficient for functions invoked (again) from Reagent.
(alter-var-root (var *print-fn*)
                (constantly print-to-output))
(alter-var-root (var *print-err-fn*)
                (constantly print-to-output))

(defn load-page-source []
  (-> (js/fetch js/window.location.href)
      (.then #(.text %))
      (.then #(reset! page-source %))))

(defn toggle-page-source []
  (if @page-source
    (reset! page-source nil)
    (load-page-source)))

(def editor-view
  (atom nil))

(defn editor-contents []
  (when-let [view @editor-view]
    (.toString (.-doc (.-state view)))))

(defn copy-to-clipboard [s]
  (let [temp-textarea (js/document.createElement "textarea")]
    (.appendChild js/document.body temp-textarea)
    (set! (.-value temp-textarea) s)
    (.select temp-textarea)
    (js/document.execCommand "copy")
    (.removeChild js/document.body temp-textarea)
    nil))

(defn encode [code]
  ;; Unfortunately the encoded URL takes quite a bit more bytes than
  ;; the code itself. I considered using Compression Streams API to
  ;; decrease URL size, but it seems relatively new and I'm not too keen
  ;; on dealing with the asynchrony.
  (-> code
      js/encodeURIComponent
      js/unescape
      js/btoa))

(defn decode [coded]
  (-> coded
      js/atob
      js/escape
      js/decodeURIComponent))

(defn location->url []
  (js/URL. js/window.location.href))

(def code-url-parameter
  "code")

(def checksum-url-parameter
  "checksum")

(defn code-checksum-id [code]
  (-> code hash js/btoa))

(defn share []
  (let [code (editor-contents)
        coded (encode code)
        url (location->url)]
    (.set (.-searchParams url)
          code-url-parameter
          coded)
    (.set (.-searchParams url)
          checksum-url-parameter
          (code-checksum-id code))
    (println "Sharing as" (str url))
    (copy-to-clipboard (str url))
    (js/alert "URL with code copied to clipboard.")))

(defn code-queryparam-value []
  (.get (.-searchParams (location->url))
        code-url-parameter))

(defn checksum-queryparam-value []
  (.get (.-searchParams (location->url))
        checksum-url-parameter))

(defn url-embedded-code []
  (when-let [coded (code-queryparam-value)]
    (let [code (decode coded)
          checksum (checksum-queryparam-value)]
      (if (or (string/blank? checksum)
              (not= checksum (code-checksum-id code)))
        (do
          (js/alert "Unfortunately, the opened URL is incomplete.\nThe embedded code fragment will be ignored.\nYou might want to try that again!")
          (.replace js/window.location (.-pathname (location->url)))
          nil)
        code))))

(def default-local-store-code-key
  "buffer-1")

(def local-store-code-key
  (atom default-local-store-code-key))

(defn local-store-history-key []
  ;; Storing the history is no longer supported, but the key is still
  ;; kept, so that any old and incompatible history can be cleaned up.
  (str @local-store-code-key ".history"))

(defn local-storage []
  (.-localStorage js/window))

(defn stored-code []
  (.getItem (local-storage) @local-store-code-key))

(defn store-code [code]
  (.setItem (local-storage)
            @local-store-code-key
            code)
  (.removeItem (local-storage) ; remove old history if there
               (local-store-history-key)))

(defn determine-initial-code []
  (if-let [url-embedded (url-embedded-code)]
    (do
      (reset! local-store-code-key
              (str "buffer-" (code-checksum-id url-embedded)))
      (let [locally-stored (stored-code)
            keep-local?
            (and locally-stored
                 (or (= locally-stored url-embedded)
                     (js/confirm "Keep previous changes for this URL?")))]
        (if keep-local?
          locally-stored
          (do
            (store-code url-embedded)
            url-embedded))))
    (or (stored-code)
        preloaded-code)))

(defn store-on-change []
  (.of (.-updateListener js/cm_view.EditorView)
       (fn [update]
         (when (.-docChanged update)
           (-> (editor-contents) store-code)))))

(defn new-editor-state [doc]
  (.create js/cm_state.EditorState
           #js {:doc doc
                :extensions #js[(.of js/cm_language.indentUnit " ")
                                js/codemirror.basicSetup
                                (js/lang_clojure.clojure)
                                (js/codemirror6_parinfer.parinferExtension)
                                (store-on-change)]}))

(defn new-editor [doc]
  (let [parent-div (.getElementById js/document "code-input")
        start-state (new-editor-state doc)]
    (js/cm_view.EditorView. #js{:state start-state
                                :parent parent-div})))

(defn initialize-code-mirror []
  (let [doc (determine-initial-code)
        view (new-editor doc)]
    (reset! editor-view view)))

(defn undo []
  (js/cm_commands.undo @editor-view)
  (.focus @editor-view))

(defn redo []
  (js/cm_commands.redo @editor-view)
  (.focus @editor-view))

(defn clear-code []
  (when (js/confirm "Really clear code buffer?")
    (.setState @editor-view (new-editor-state ""))
    (store-code ""))
  (.focus @editor-view))

(defn -clear-output! []
  (reset! output "")
  (reset! reagent-component nil))

(defn scroll-output-into-focus! []
  (let [dom-node (.getElementById js/document "output")]
    (.scrollIntoView dom-node {"block" "end", "behavior" "smooth"})))

;; available to user (renders within div)
(defn render-with-reagent [component]
  (reset! reagent-component component))

(defonce external-sources (atom {}))

(declare load-code)

(defn load-from-url
  "Experimental; necessarily asynchronous, so fetched code only becomes
   available on reload, which is forced as soon as code is loaded (so, any code
   before this invocation might be evaluated more than once).
   E.g. (load-from-url \"https://raw.githubusercontent.com/weavejester/medley/master/src/medley/core.cljc\")"
  [url]
  (if-let [source (get @external-sources url)]
    (load-string source)
    (do
      (println "Loading" url "...")
      (-> (js/fetch url)
          (.then #(.text %))
          (.then (fn [source]
                   (swap! external-sources assoc url source)
                   (load-string source)
                   (println "Loaded" url)
                   (load-code)))) ; a bit crude; let's see
      (throw (ex-info "loading" {:loading-url url})))))

(defn load-code []
  (let [c (editor-contents)]
    (-clear-output!)
    (js/setTimeout
     (fn []
       (try
         (let [value (load-string c)]
           (print-to-output (pr-str value)))
         (catch ^:sci/error js/Error e
           (when-not (contains? (ex-data e) :loading-url)
             (println "Error:" (.-message e))
             (run! println (-> (sci/stacktrace e) (sci/format-stacktrace))))))
       (js/setTimeout scroll-output-into-focus! 100)
       200))))

(defn render-output []
  (when-not (nil? @output)
    [:div {:style {:margin-top "1em"}}
     [:h3 "Output"]
     [:div {:id "output"}
      [:pre {:id "output-text"} @output]]
     [:button
      {:style {:margin "1px"}
       :on-click #(do (copy-to-clipboard @output)
                      (js/alert "Output copied to clipboard."))}
      "Copy to clipboard"]]))

(defn render-reagent-component []
  (when-let [component @reagent-component]
    [:div {:style {:margin-top "1em"
                   :margin-bottom "2em"}}
     [:h3 "Render"]
     [:div
      (if (vector? component)
        component
        [component])]]))

(defn github-link []
  [:a {:class "github-button"
       :href "https://github.com/jurjanpaul/ape-cljs-playground"
       :data-icon="octicon-repo"}
   ""])

(defn horizontal-separator []
  [:span
   {:style {:margin-left "0.2em"
            :margin-right "0.2em"
            :color "grey"}}
   "|"])

(defn editor []
  [:div
   [:div
    {:style {:display "flex"}}
    [:div
     [:h3
      "ðŸ¦§ ape-cljs-playground "]]
    [:div
     {:style {:display "flex"
              :position "absolute"
              :right "0px"
              :top "0px"
              :margin-top "0.5em"}}
     [:button
      {:style {:margin-left "4px"
               :margin-right "4px"
               :margin-bottom "0.5em"}
       :on-click toggle-page-source}
      "source with docs " (if @page-source "â–½" "â–·")]
     [:span {:style {:padding-top "1px"
                     :margin-right "4px"}}
      [github-link]]]]
   [:p
    {:style {:font-size "0.9em"}}
    [:span [:i "Away from Preferred Editor"] " ClojureScript Playground"]]
   (when @page-source
     [:div
      {:style {:font-family "Source Code Pro, monospace"
               :font-size "0.6rem"
               :margin-bottom "1em"}}
      [:pre
       {:style {:padding "0.5em"
                :min-width "40em"
                :overflow "auto"
                :border-width "1px"
                :border-style "solid"
                :border-color "green"
                :color "green"}}
       @page-source]])
   [:div
    {:id "code-input"}]
   [:div
    [:button
     {:style {:margin "1px"}
      :on-click load-code}
     "Load"]
    [horizontal-separator]
    [:button
     {:style {:margin "0 1px 0 1px"
              :padding "0 4px 0 4px"
              :font-size "1.2em"
              :font-weight "bold"
              :transform "translate(0,2px)"}
      :on-click undo}
     "âŸ²"]
    [:button
     {:style {:margin "0 1px 0 1px"
              :padding "0 4px 0 4px"
              :font-size "1.2em"
              :font-weight "bold"
              :transform "translate(0,2px)"}
      :on-click redo}
     "âŸ³"]
    [horizontal-separator]
    [:button
     {:style {:margin "1px"}
      :on-click #(do (copy-to-clipboard (editor-contents))
                     (js/alert "Code copied to clipboard."))}
     "Copy"]
    [:button
     {:style {:margin "1px"}
      :on-click share}
     "Share"]
    [:button
     {:style {:margin "1px"}
      :on-click clear-code}
     "Clear"]
    (when-not (string/blank? (.-search (location->url)))
      [:<>
       [horizontal-separator]
       [:span
        {:style {:font-size "0.8em"}}
        [:a
         {:href (.-pathname (location->url))}
         "main buffer >"]]])]
   [render-output]
   [render-reagent-component]])

(rdom/render [editor] (.getElementById js/document "editor"))
(initialize-code-mirror)
   </script>
  </head>
  <body>
    <div id="editor"></div>
    <script async defer src="https://buttons.github.io/buttons.js"></script>
  </body>
</html>
