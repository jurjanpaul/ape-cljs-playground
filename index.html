<html>
  <!--
ape-cljs-playground - Away from Preferred Editor ClojureScript Playground

This page allows you to simply try out Clojure forms on your mobile device in
moments when you don't have your laptop/desktop with preferred text editor at
hand.

Therefore, this little project aims to provide mobile-friendly Clojure editing.
To me this means (at least):
  - Parinfer!
  - kept in (local) storage to allow for quick and easy incremental work;
  - evaluate anything that's available in (the SCI subset of) ClojureScript,
    including macros within the browser;
  - all output (including errors) displayed on the page.

Quite a number of web-based/mobile-oriented Clojure editors exist, but I haven't
yet found any that support all of the above sub-goals 'out of the box',
particularly the storage part. So, that's the itch being scratched here.

Not particularly aiming for
 - performance (just spoiled by SCI's out-of-the-box speed I guess);
 - small download size.

The project's git repository can be found at
https://github.com/jurjanpaul/ape-cljs-playground.
The page is hosted at https://jurjanpaul.github.io/ape-cljs-playground.

Currently provides
 - run ClojureScript (per SCI / Scittle)
   - includes macro support out-of-the-box (might require an extra reload)
 - Parinfer (per parinfer-codemirror)
 - persistence (per browser's localStorage)
 - Reagent (not essential, but did not require any extra effort per Scittle,
   plus it definitely kickstarted some dogfeeding).
   To render, simply invoke the convenience function `render-with-reagent`
   with a reagent component function as parameter.
 - maximum portability by being contained in a single HTML file
   (not al all essential, but nice as long as it's feasible in light of the
   remaining wishes)
 - trivial, but very helpful: can be saved as an 'app' on phone's home screen.

Current annoyances
 - After (un)zooming&scrolling, the cursor position sometimes no longer
   corresponds to the actual insertion point;
 - Error logs don't provide position info (line and column);
 - Losing editor/viewport focus when deleting/commenting code.

Other wishes/TODOs (nothing planned):
 - history / undo + redo
 - on-screen footer keyboard with opening braces + other useful special chars
 - multiple buffers
   (Hint: by saving the page multiple times to my iPhone's Home screen I already
   get multiple buffers for free!)
 - improved styling for mobile (just zoom and swipe/scroll a lot for now!);
 - linting
 - allow fetching existing code (gist) from url
 - support links with the code in a query parameter (tricky to get right in
   combination with the local storage, not wanting to overwrite any previously
   entered code without warning);
 - add deps (seems hard/hardly possible)
 - separate Scittle script's namespace from user entered code's namespace
 - save to gist or ... ?
 - upgrade to CodeMirror 6 (support for Parinfer seems to be a challenge still)

API
 - everything that Scittle exposes
 - render-with-reagent
 - load-from-url (experimental)

All in all I feel this is a nice showcase of what is possible with Scittle!
Thanks to all library and tool builders who made it possible for me to throw
this together so lazily!

Feel free to customize and extend this file as desired. Of course, I would like
to hear if this turns out helpful to you in any way.

  Jurjan-Paul Medema, August 2022
  Github: jurjanpaul
  Twitter: @jurjanpaul
  Clojurians Slack: @Jurjan-Paul Medema

P.S. Some of the alternative mobile / in-browser Clojure(Script) 'playgrounds'
that I know of:
  * http://app.klipse.tech/
  * https://borkdude.github.io/sci.web/
  * https://clojurescript.io/
  * Replete (iPhone app)

('Ape' also refers to the fact that this can indeed be considered a poor copy or
derivative of other people's hard work.)
-->
  <head>
    <title>Away from Preferred Editor ClojureScript Playground</title>
    <meta name="apple-mobile-web-app-capable" content="yes">
    <meta name="mobile-web-app-capable" content="yes">
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <meta name="url" content="https://jurjanpaul.github.io/ape-cljs-playground">
    <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/lib/codemirror.css">
    <style type="text/css">
      html, body, div, p, pre, h3, textarea {
        box-sizing: border-box;
        margin: 0;
        padding: 0;
      }

      body {
        padding: 1px 4px;
        font-family: sans-serif;
      }

      h3 {
        margin-top: 1em;
      }

      .parinfer-error {
        background: #FFBEBE;
        color: black !important;
      }

      .parinfer-paren-trail {
        opacity: 0.4;
      }

      .CodeMirror {
        box-sizing: border-box;
        border: 1px solid #aaa;
        padding: 1px;
        padding-bottom: 1em;
      }

      .CodeMirror-matchingbracket {
        color: orange !important;
        font-weight: bold;
        background-color: lightgray;        ;
      }

      .output {
        font-family: monospace;
        padding: 4px 4px;
        padding-bottom: 1em;
        color: white;
        background-color: darkslateblue;
        font-size: small;
        overflow-y: scroll;
      }

      .page-source {
        border-style: solid;
        border-width: 1px;
        margin: 1px;
        padding: 1px;
      }
    </style>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/lib/codemirror.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/addon/edit/matchbrackets.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/mode/clojure/clojure.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/parinfer@3.12.0/parinfer.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/parinfer-codemirror@1.4.2/parinfer-codemirror.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.2.8/dist/scittle.js" type="application/javascript"></script>
    <script crossorigin src="https://unpkg.com/react@17/umd/react.production.min.js"></script>
    <script crossorigin src="https://unpkg.com/react-dom@17/umd/react-dom.production.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.2.8/dist/scittle.reagent.js" type="application/javascript"></script>
    <script type="application/x-scittle">
(require '[clojure.string :as string]
         '[reagent.core :as r]
         '[reagent.dom :as rdom])

(def preloaded-code
  "(defn answer []
  ;; Better not obfuscate like this
  (->> js/Math.PI int (nth (iterate #(* % (inc %)) 1))))

(defn the-answer []
  [:div
   [:p \"The answer is ... \" (answer)]])

(render-with-reagent the-answer)

(load-from-url \"https://raw.githubusercontent.com/weavejester/medley/master/src/medley/core.cljc\")
(pr (medley.core/index-by :id [{:id 1, :value :a} {:id 2, :value :b}]))

(answer)
")
;; end of preloaded-code

(def page-source (r/atom nil))
(def reagent-component (r/atom nil))
(def output (r/atom ""))
(defn print-to-output [s]
  (swap! output str s "\n"))

;; Originally went with just `binding` `*print-fn*` and `*print-err-fn*`, but
;; that failed to be sufficient for functions invoked (again) from Reagent.
(alter-var-root (var *print-fn*)
                (constantly print-to-output))
(alter-var-root (var *print-err-fn*)
                (constantly print-to-output))

(defn load-page-source []
  (-> (js/fetch js/window.location.href)
      (.then #(.text %))
      (.then #(reset! page-source %))))

(defn toggle-page-source []
  (if @page-source
    (reset! page-source nil)
    (load-page-source)))

(def code-mirror
  (atom nil))

(defn code []
  (when-let [cm @code-mirror]
    (.getValue cm)))

(defn copy-to-clipboard [s]
  (let [temp-textarea (js/document.createElement "textarea")]
    (.appendChild js/document.body temp-textarea)
    (set! (.-value temp-textarea) s)
    (.select temp-textarea)
    (js/document.execCommand "copy")
    (.removeChild js/document.body temp-textarea)))

(def local-store-code-key "buffer-1")

(defn stored-code []
  (.getItem (.-localStorage js/window) local-store-code-key))

(defn store-code [code]
  (.setItem (.-localStorage js/window) local-store-code-key code))

(defn code-input-changed [_event]
  (-> (code) store-code))

(defn initialize-code-mirror []
  (reset! code-mirror
          (.fromTextArea js/CodeMirror
                         (.getElementById js/document "code-input")
                         #js {"mode" "clojure"
                              "lineNumbers" true
                              "matchBrackets" true}))
  (.on @code-mirror
       "change"
       (fn [_ change] (code-input-changed change)))
  (.setValue @code-mirror
             (or (stored-code)
                 preloaded-code))
  (.init js/parinferCodeMirror @code-mirror "smart" {}))

(defn clear-code []
  (when (js/confirm "Really clear code buffer?")
    (.setValue @code-mirror "")
    (store-code "")))

(defn -clear-output! []
  (reset! output "")
  (reset! reagent-component nil))

;; available to user (renders within div)
(defn render-with-reagent [component]
  (reset! reagent-component component))

(defonce external-sources (atom {}))

(declare load-code)

(defn load-from-url [url]
  "Experimental; necessarily asynchronous, so fetched code only becomes
   available on reload, which is forced as soon as code is loaded (so, any code
   before this invocation might be evaluated more than once).
   E.g. (load-from-url \"https://raw.githubusercontent.com/weavejester/medley/master/src/medley/core.cljc\")"
  (if-let [source (get @external-sources url)]
    (load-string source)
    (do
      (println "Loading" url "...")
      (-> (js/fetch url)
          (.then #(.text %))
          (.then (fn [source]
                   (swap! external-sources assoc url source)
                   (load-string source)
                   (println "Loaded" url)
                   (load-code)))) ; a bit crude; let's see
      (throw (ex-info "loading" {:loading-url url})))))

(defn load-code []
  (let [c (code)]
    (try
      (-clear-output!)
      (let [value
            #_(js/scittle.core.eval_string c)
            (load-string c)]
        (print-to-output (pr-str value)))
      (catch :default e
        (when-not (contains? (ex-data e) :loading-url)
          (println e)
          (when-let [stack (.-stack e)]
            (println stack))
          (throw e))))))

(defn editor []
  [:div
   [:h3
    "ape-cljs-playground "
    [:button
     {:style {:font-size "0.8rem", :margin "1px", :padding "0"}
      :on-click toggle-page-source}
     "</>"]]
   [:i "Away from Preferred Editor ClojureScript Playground"]
   (when @page-source
     [:div.page-source
      [:pre @page-source]])
   [:textarea
    {:id "code-input"
     :cols 120
     :rows 20
     :onChange code-input-changed}
    (or (.getItem (.-localStorage js/window) "code")
        preloaded-code)]
   [:div
    [:button
     {:on-click load-code}
     (if (string/blank? @output)
       "Load"
       "Reload")]
    [:button
     {:style {:margin "1px"}
      :on-click #(copy-to-clipboard (code))}
     "Copy to clipboard"]
    [:button
     {:style {:margin "1px"}
      :on-click clear-code}
     "Clear code"]]
   (when-not (string/blank? @output)
     [:div
      [:h3 "Output"]
      [:div.output
       [:pre @output]]])
   (when-let [component @reagent-component]
     [:div
      [:h3 "Render"]
      (if (vector? component)
        component
        [component])])])

(rdom/render [editor] (.getElementById js/document "editor"))
(initialize-code-mirror)
    </script>
  </head>
  <body>
    <div id="editor"></div>
  </body>
</html>
