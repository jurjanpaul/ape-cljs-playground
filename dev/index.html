<html>
  <!--
ape-cljs-playground - Away from Preferred Editor ClojureScript Playground

This page allows you to simply try out Clojure forms on your mobile device in
moments when you don't have your laptop/desktop with preferred text editor at
hand. (You can also, for example, use it to write and run solutions for all of
the Advent of Code puzzles, https://adventofcode.com/, as I did for the 2023
edition.)

Therefore, this little project aims to provide mobile-friendly Clojure editing.
To me this means (at least):
  - Parinfer!
  - kept in (local) storage to allow for quick and easy incremental work;
  - evaluate anything that's available in (the SCI subset of) ClojureScript,
    including macros within the browser;
  - all output (including errors) displayed on the page.

Quite a number of web-based/mobile-oriented Clojure editors exist, but I haven't
yet found any that support all of the above sub-goals 'out of the box',
particularly the storage part. So, that's the itch being scratched here.

Not particularly aiming for
 - performance (just spoiled by SCI's out-of-the-box speed I guess);
 - small download size.

The project's git repository can be found at
https://github.com/jurjanpaul/ape-cljs-playground.
The page is hosted at https://jurjanpaul.github.io/ape-cljs-playground.

Currently provides
 - run ClojureScript (per SCI / Scittle, https://github.com/babashka/scittle)
   - includes macro support out-of-the-box (might require an extra reload)
 - Parinfer (per parinfer-codemirror)
 - persistence (per browser's localStorage)
 - Reagent (not essential, but did not require any extra effort per Scittle,
   plus it definitely kickstarted some dogfeeding).
   To render, simply invoke the convenience function `render-with-reagent`
   with a reagent component function as parameter.
 - maximum portability by being contained in a single HTML file
   (not at all essential, but nice as long as it's feasible in light of the
   remaining wishes)
   => Well, this is no longer really true anymore now that I'm depending on
      a tweaked version of parinfer-codemirror.js to improve scrolling
      behaviour.
 - trivial, but very helpful: can be saved as an 'app' on phone's home screen,
   each 'app' automagically getting its own localStorage!
 - history / undo + redo

Current annoyances
 - After (un)zooming&scrolling, the cursor position sometimes no longer
   corresponds to the actual insertion point;
   => Unfortunately, I still haven't figured out how to deal with this,
   other than:
    - zooming out;
    - selecting another page element;
    - tilting the phone;
    - reloading the entire page...
 - Cursor position being scrolled out of focus when deleting/commenting code.
   => Improved the situation  w.r.t. vertical scrolling somewhat by tweaking
      parinfer-codemirror.js

Other wishes/TODOs (nothing planned):
 - on-screen footer keyboard with opening braces + other useful special chars
 - multiple buffers
   (Hint: by saving the page multiple times to my iPhone's Home screen I
   already get multiple buffers for free!)
 - improved styling for mobile (just zoom and swipe/scroll a lot for now!);
 - linting
 - allow fetching existing code (gist) from url
 - support links with the code in a query parameter (tricky to get right in
   combination with the local storage, not wanting to overwrite any previously
   entered code without warning);
 - add deps (seems hard/hardly possible)
 - separate Scittle script's namespace from user entered code's namespace
 - save to gist or ... ?
 - upgrade to CodeMirror 6 (support for Parinfer seems to be a challenge
   still)

API
 - everything that Scittle exposes, including:
     - cljs.pprint as pprint
     - clojure.string as string
     - reagent.core as r
     - reagent.dom as rdom
 - render-with-reagent
 - load-from-url (experimental)

All in all I feel this is a nice showcase of what is possible with Scittle!
Thanks to all library and tool builders who made it possible for me to throw
this together so lazily!

Feel free to customize and extend this file as desired. Of course, I would
like to hear if this turns out helpful to you in any way.

  Jurjan-Paul Medema
  Github: jurjanpaul
  Twitter: @jurjanpaul
  Clojurians Slack: @jurjanpaul

P.S. Some of the alternative mobile / in-browser Clojure(Script) 'playgrounds'
that I know of:
  * http://app.klipse.tech/
  * https://borkdude.github.io/sci.web/
  * https://clojurescript.io/
  * https://nextjournal.github.io/clojure-mode/#try-it
  * https://oakes.github.io/paren-soup/
  * https://squint-cljs.github.io/squint/
  * Replete (iPhone app)

('Ape' also refers to the fact that this can indeed be considered a poor copy or
derivative of other people's hard work.)
-->
  <head>
    <title>Away from Preferred Editor ClojureScript Playground</title>
    <meta name="apple-mobile-web-app-capable" content="yes">
    <meta name="mobile-web-app-capable" content="yes">
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <meta name="url" content="https://jurjanpaul.github.io/ape-cljs-playground">
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Open+Sans:wght@400;700&family=Source+Code+Pro&display=swap" rel="stylesheet">
    <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/lib/codemirror.css">
    <style type="text/css">
      * {
        box-sizing: border-box;
        margin: 0;
        padding: 0;
      }

      body {
        padding: 4px 4px 2em;
        font-family: "Open Sans", sans-serif;
      }

      button {
        border-radius: 4px;
        padding: 2px 4px 2px 4px;
      }

      .parinfer-error {
        background: #FFBEBE;
        color: black !important;
      }

      .parinfer-paren-trail {
        opacity: 0.4;
      }

      .CodeMirror {
        box-sizing: border-box;
        resize: both;
        border: 1px solid #aaa;
        padding: 1px;
        padding-bottom: 1em;
        margin-bottom: 2px;
      }

      .CodeMirror, .CodeMirror * {
         font-family: "Source Code Pro", monospace;
         font-size: 0.8rem;
      }

      .CodeMirror-matchingbracket {
        color: orange !important;
        font-weight: bold;
        background-color: lightgray;        ;
      }

      #output {
        font-family: "Source Code Pro", monospace;
        padding: 0.5em;
        padding-bottom: 1em;
        color: white;
        background-color: #120754;
        font-size: small;
        overflow-y: scroll;
        min-height: 10em;
        border-style: inset;
        border-width: 1px;
        border-radius: 5px;
        border-color: lightblue;
        resize: both;
      }

      .page-source {
        border-style: solid;
        border-width: 1px;
        padding: 0.5em;
        margin-bottom: 0.5em;
      }

      .pointer {
        cursor: pointer;
      }
    </style>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.15/dist/scittle.js" type="application/javascript"></script>
    <script crossorigin src="https://unpkg.com/react@17/umd/react.production.min.js"></script>
    <script crossorigin src="https://unpkg.com/react-dom@17/umd/react-dom.production.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.15/dist/scittle.reagent.js" type="application/javascript"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.15/dist/scittle.re-frame.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.15/dist/scittle.pprint.js" type="application/javascript"></script>
    <script type="application/x-scittle">
(require '[cljs.pprint :as pprint]
         '[clojure.string :as string]
         '[reagent.core :as r]
         '[reagent.dom :as rdom]
         '[sci.core :as sci])

(def preloaded-code
  "(defn answer []
  ;; Better not obfuscate like this
  (->> js/Math.PI int (nth (iterate #(* % (inc %)) 1))))

(defn the-answer []
  [:div {:style {:font-family \"Georgia\"}}
   [:p \"The answer turns out to ðŸ ... \" (answer)]])

(render-with-reagent the-answer)

(load-from-url \"https://raw.githubusercontent.com/weavejester/medley/1.5.0/src/medley/core.cljc\") ; master doesn't work (reader conditionals)
(pr (medley.core/index-by :id [{:id 1, :value :a} {:id 2, :value :b}]))

(answer)
")
;; end of preloaded-code

(def page-source (r/atom nil))
(def reagent-component (r/atom nil))
(def output (r/atom nil))
(defn print-to-output [s]
  (swap! output str s "\n"))

;; Originally went with just `binding` `*print-fn*` and `*print-err-fn*`, but
;; that failed to be sufficient for functions invoked (again) from Reagent.
(alter-var-root (var *print-fn*)
                (constantly print-to-output))
(alter-var-root (var *print-err-fn*)
                (constantly print-to-output))

(defn load-page-source []
  (-> (js/fetch js/window.location.href)
      (.then #(.text %))
      (.then #(reset! page-source %))))

(defn toggle-page-source []
  (if @page-source
    (reset! page-source nil)
    (load-page-source)))

(def code-mirror
  (atom nil))

(defn code []
  (when-let [cm @code-mirror]
    (.getValue cm)))

(defn history []
  (when-let [cm @code-mirror]
    (.getHistory (.getDoc cm))))

(defn copy-to-clipboard [s]
  (let [temp-textarea (js/document.createElement "textarea")]
    (.appendChild js/document.body temp-textarea)
    (set! (.-value temp-textarea) s)
    (.select temp-textarea)
    (js/document.execCommand "copy")
    (.removeChild js/document.body temp-textarea)
    nil))

(defn encode [code]
  ;; Considered using Compression Streams API to decrease URL size, but decided that
  ;; is probably still too new to rely on its presence on all devices.
  (-> code
      js/encodeURIComponent
      js/unescape
      js/btoa))

(defn decode [coded]
  (-> coded
      js/atob
      js/escape
      js/decodeURIComponent))

(defn location->url []
  (js/URL. js/window.location.href))

(defn src-queryparam-value []
  (.get (.-searchParams (location->url)) "src"))

(defn share []
  (let [coded (encode (code))
        url (location->url)]
    (.set (.-searchParams url) "src" coded)
    (println (str url))
    (copy-to-clipboard (str url))))

(defn url-embedded-code []
  (when-let [src (src-queryparam-value)]
    (decode src)))

(def default-local-store-code-key
  "buffer-1")

(def local-store-code-key
  (atom default-local-store-code-key))

(defn local-store-history-key []
  (str @local-store-code-key ".history"))

(defn local-storage []
  (.-localStorage js/window))

(defn stored-code []
  (.getItem (local-storage) @local-store-code-key))

(defn store-code [code]
  (.setItem (local-storage)
            @local-store-code-key
            code))

(defn stored-history []
  (js/JSON.parse (.getItem (local-storage)
                           (local-store-history-key))))

(defn store-history [history]
  (.setItem (local-storage)
            (local-store-history-key)
            (js/JSON.stringify history)))

(def store-history-timeout-id (atom nil))

(defn planned-store-history []
  (when @store-history-timeout-id
    (js/clearTimeout @store-history-timeout-id)
    (reset! store-history-timeout-id nil))
  (-> (history)
      store-history))

(defn plan-store-history []
  (when @store-history-timeout-id
    (js/clearTimeout @store-history-timeout-id)
    (reset! store-history-timeout-id nil))
  (reset! store-history-timeout-id
          (js/setTimeout planned-store-history 400)))

(defn code-input-changed [_event]
  (-> (code) store-code)
  (plan-store-history))

(defn determine-initial-code []
  (if-let [url-embedded (url-embedded-code)]
    (do
      (reset! local-store-code-key
              (str "buffer-" (hash url-embedded))) ; Clojure's hash seems ok for now?!
      (let [locally-stored (stored-code)
            keep-local?
            (and locally-stored
                 (or (= locally-stored url-embedded)
                     (js/confirm "Keep previous changes for this URL?")))]
        (if keep-local?
          locally-stored
          (do
            (store-history "")
            (store-code url-embedded)
            url-embedded))))
    (or (stored-code)
        preloaded-code)))

(defn initialize-code-mirror []
  (reset! code-mirror
          (.fromTextArea js/CodeMirror
                         (.getElementById js/document "code-input")
                         #js {"mode" "clojure"
                              "lineNumbers" true
                              "matchBrackets" true}))
  (.init js/parinferCodeMirror @code-mirror "smart" {})
  (.on @code-mirror
       "change"
       (fn [_ change] (code-input-changed change)))
  (.setValue @code-mirror
             (determine-initial-code))
  (when-let [history (stored-history)]
    (.setHistory (.getDoc @code-mirror) history)))

(defn undo []
  (.execCommand @code-mirror "undo"))

(defn redo []
  (.execCommand @code-mirror "redo"))

(defn clear-code []
  (when (js/confirm "Really clear code buffer?")
    (.setValue @code-mirror "")
    (store-code "")))

(defn -clear-output! []
  (reset! output "")
  (reset! reagent-component nil))

(defn scroll-output-into-focus! []
  (let [dom-node (.getElementById js/document "output")]
    (.scrollIntoView dom-node {"block" "end", "behavior" "smooth"})))

;; available to user (renders within div)
(defn render-with-reagent [component]
  (reset! reagent-component component))

(defonce external-sources (atom {}))

(declare load-code)

(defn load-from-url [url]
  "Experimental; necessarily asynchronous, so fetched code only becomes
   available on reload, which is forced as soon as code is loaded (so, any code
   before this invocation might be evaluated more than once).
   E.g. (load-from-url \"https://raw.githubusercontent.com/weavejester/medley/master/src/medley/core.cljc\")"
  (if-let [source (get @external-sources url)]
    (load-string source)
    (do
      (println "Loading" url "...")
      (-> (js/fetch url)
          (.then #(.text %))
          (.then (fn [source]
                   (swap! external-sources assoc url source)
                   (load-string source)
                   (println "Loaded" url)
                   (load-code)))) ; a bit crude; let's see
      (throw (ex-info "loading" {:loading-url url})))))

(defn load-code []
  (let [c (code)]
    (-clear-output!)
    (js/setTimeout
     (fn []
       (try
         (let [value (load-string c)]
           (print-to-output (pr-str value)))
         (catch ^:sci/error js/Error e
           (when-not (contains? (ex-data e) :loading-url)
             (println "Error:" (.-message e))
             (run! println (-> (sci/stacktrace e) (sci/format-stacktrace))))))
       (js/setTimeout scroll-output-into-focus! 100))
       200)))

(defn render-output []
  (when-not (nil? @output)
    [:div {:style {:margin-top "1em"}}
     [:h3 "Output"]
     [:div {:id "output"}
      [:pre {:id "output-text"} @output]]
     [:button
      {:style {:margin "1px"}
       :on-click #(do (copy-to-clipboard @output)
                      (js/alert "Output copied to clipboard."))}
      "Copy to clipboard"]]))

(defn render-reagent-component []
  (when-let [component @reagent-component]
    [:div {:style {:margin-top "1em"
                   :margin-bottom "2em"}}
     [:h3 "Render"]
     [:div
      (if (vector? component)
        component
        [component])]]))

(defn github-link []
  [:a {:class "github-button"
       :href "https://github.com/jurjanpaul/ape-cljs-playground"
       :data-icon="octicon-repo"}
   ""])

(defn title-clicked [_]
  (let [url (location->url)]
    (println "new-location:" (.-pathname url))
    (js/window.location.replace (.-pathname url))))

(defn editor []
  [:div
   [:div
    {:style {:display "flex"}}
    [:div
     [:h3
      {:style {:cursor "pointer"}
       :on-click title-clicked}
      "ðŸ¦§ ape-cljs-playground "]]
    [:div
     {:style {:display "flex"
              :position "absolute"
              :right "0px"
              :top "0px"
              :margin-top "0.5em"}}
     [:span {:style {:padding-top "1px"}}
      [github-link]]
     [:button
      {:style {:margin-left "4px"
               :margin-right "4px"
               :margin-bottom "0.5em"}
       :on-click toggle-page-source}
      "source with docs " (if @page-source "â–½" "â–·")]]]
   [:p
    {:style {:font-size "0.9em"}}
    [:span [:i "Away from Preferred Editor"] " ClojureScript Playground"]]
   (when @page-source
     [:div
      {:style {:font-family "Source Code Pro, monospace"
               :font-size "0.6rem"
               :margin-bottom "1em"}}
      [:pre
       {:style {:padding "0.5em"
                :min-width "40em"
                :overflow "auto"
                :border-width "1px"
                :border-style "solid"
                :border-color "green"
                :color "green"}}
       @page-source]])
   [:textarea
    {:id "code-input"
     :cols 120
     :rows 20
     :onChange code-input-changed}
    (or (.getItem (.-localStorage js/window) "code")
        preloaded-code)]
   [:div
    [:button
     {:style {:margin "1px"}
      :on-click load-code}
     (if (string/blank? @output)
       "Load"
       "Reload")]
    [:span.pointer
     {:style {:margin "2px"}
      :on-click undo}
     "â†©ï¸"]
    [:span.pointer
     {:style {:margin "2px"}
      :on-click redo}
     "â†ªï¸"]
    [:span
     {:style {:margin-left "0.3em"
              :margin-right "0.3em"
              :color "grey"}}
     "|"]
    [:button
     {:style {:margin "1px"}
      :on-click #(do (copy-to-clipboard (code))
                     (js/alert "Code copied to clipboard."))}
     "Copy"]
    [:button
     {:style {:margin "1px"}
      :on-click #(do (share)
                     (js/alert "URL with code copied to clipboard."))}
      "Share"]
    [:button
     {:style {:margin "1px"}
      :on-click clear-code}
     "Clear"]
    (when-not (string/blank? (.-search (location->url)))
      [:span
       {:style {:font-size "0.8em"
                :margin-left "0.3em"}}
       [:a
        {:href (str (.-pathname (location->url)))}
        "default buffer"]])]
   [render-output]
   [render-reagent-component]])

(rdom/render [editor] (.getElementById js/document "editor"))
(initialize-code-mirror)
    </script>
  </head>
  <body>
    <div id="editor"></div>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/lib/codemirror.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/addon/edit/matchbrackets.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/codemirror@5.65.6/mode/clojure/clojure.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/parinfer@3.12.0/parinfer.min.js"></script>
    <!-- <script src="https://cdn.jsdelivr.net/npm/parinfer-codemirror@1.4.2/parinfer-codemirror.min.js"></script> -->
    <script src="lib/parinfer-codemirror.js" type="application/javascript"></script> <!-- tweaked version w.r.t. scrolling -->
    <script async defer src="https://buttons.github.io/buttons.js"></script>
  </body>
</html>
